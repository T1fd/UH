{
 'Реквизиты': {
  'Наименование': 'Загрузка факта БДР Т1И (из УХ) с 01.10.2021',
  'ТекстОбработки': '	МесяцЗагрузки = Текущаядата();    
	МесяцевЗагрузки = 2; 
	ДатаНачалаЗагрузки = Дата(2021,10,1); //не ранее этой даты
	
	ЗагружатьДанныеТолькоПоПроектам = Истина;
	
	СоответствиеСтатейБДР = Новый Соответствие;
	СоответствиеСтатейБДР.Вставить("05010101", Справочники.СтатьиДоходовИРасходов.НайтиПоКоду("11020100"));
	СоответствиеСтатейБДР.Вставить("05010102", Справочники.СтатьиДоходовИРасходов.НайтиПоКоду("11020100"));
	СоответствиеСтатейБДР.Вставить("05020000", Справочники.СтатьиДоходовИРасходов.НайтиПоКоду("11020200"));

	//переменные
	ВалютаРуб = Константы.ВалютаРегламентированногоУчета.Получить();
	ДокументБД = Справочники.ДокументыБД.НайтиПоНаименованию("Операция",,,Справочники.ТипыБазДанных.ТекущаяИБ);
	Сценарий = Справочники.Сценарии.Факт;
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КомментарийДокумента = "#УХСборФакта";
    
    МассивОрганизаций = новый Массив;
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("1ea74345-bd4c-11eb-96cf-005056015ee4"))); //ГРУППА ТЕХНОСЕРВ
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("0ee356a6-bd4d-11eb-96cf-005056015ee4"))); //Т1КЛАУД
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("c8ec9af6-bd4d-11eb-96cf-005056015ee4"))); //ТЕХНОСЕРВ АС ООО
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("c760179d-bd4f-11eb-96cf-005056015ee4"))); //ТЕХНОСЕРВ МЕНЕДЖМЕНТ ООО
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("98903ee0-bd50-11eb-96cf-005056015ee4"))); //ТЕХНОСЕРВЪ А/С АО
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("3330a635-bd52-11eb-96cf-005056015ee4"))); //ТЕХНОТРАНССЕРВ ООО
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("8df3b815-bd52-11eb-96cf-005056015ee4"))); //ТРЭЙД ИНВЕСТ ТЕХНОЛОДЖИЗ ООО
	МассивОрганизаций.Добавить(Справочники.Организации.ПолучитьСсылку(Новый УникальныйИдентификатор("5959cae0-bd53-11eb-96cf-005056015ee4"))); //ТС ИНТЕГРАЦИЯ ООО
	
	//вычисляемые переменные
	ДатаНачала = НачалоМесяца(ДобавитьМесяц(МесяцЗагрузки,1-МесяцевЗагрузки));
	ДатаНачала = Макс(ДатаНачала,ДатаНачалаЗагрузки);
	ДатаОкончания = КонецМесяца(МесяцЗагрузки);
    
    
	ДатаНачалаЗагрузки = Текущаядата();
	Обработано = 0;
	Загружено = 0;	
	
	//Список организаций текущей системы
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.КПП КАК КПП,
	|	Организации.ИНН КАК ИНН
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ
	|  И Организации.Ссылка в (&Организации)";
	Запрос.УстановитьПараметр("Организации", МассивОрганизаций);	
	РезультатЗапросаОрганизации = Запрос.Выполнить().Выгрузить();

	//Периоды текущей системы
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Периоды.Ссылка КАК Ссылка,
	|	Периоды.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	Справочник.Периоды КАК Периоды
	|ГДЕ
	|	Периоды.ПометкаУдаления = ЛОЖЬ
	|	И Периоды.Периодичность = &Месяц";
	Запрос.УстановитьПараметр("Месяц", Перечисления.Периодичность.Месяц);
	РезультатЗапросаПериоды = Запрос.Выполнить().Выгрузить();
         
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т1_СтатьиБДСПоЦФО.СтатьяБДС КАК СтатьяБДДС,
	|	МИНИМУМ(Т1_СтатьиБДСПоЦФО.ЦФО) КАК ЦФО
	|ИЗ
	|	РегистрСведений.Т1_СтатьиБДСПоЦФО КАК Т1_СтатьиБДСПоЦФО
	|
	|СГРУППИРОВАТЬ ПО
	|	Т1_СтатьиБДСПоЦФО.СтатьяБДС";
	РезультатЗапросаСтатьяБДДСЦФО = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Т1_СтатьиБДРПоЦФО.СтатьяБДР КАК СтатьяБДР,
	|	МИНИМУМ(Т1_СтатьиБДРПоЦФО.ЦФО) КАК ЦФО
	|ИЗ
	|	РегистрСведений.Т1_СтатьиБДРПоЦФО КАК Т1_СтатьиБДРПоЦФО       
	|
	|СГРУППИРОВАТЬ ПО
	|	Т1_СтатьиБДРПоЦФО.СтатьяБДР";
	РезультатЗапросаСтатьяБДРЦФО = Запрос.Выполнить().Выгрузить();
    
	//Запрос в БП
	//ЗапросБПЗО = Соединение.NewObject("Запрос");
	ЗапросБПЗО = Новый Запрос; 
	ЗапросБПЗО.УстановитьПараметр("Организации", МассивОрганизаций);
	ЗапросБПЗО.УстановитьПараметр("ЗагружатьДанныеТолькоПоПроектам", ЗагружатьДанныеТолькоПоПроектам);
	ЗапросБПЗО.УстановитьПараметр("НачалоПериода", НачалоДня(ДатаНачала));
	ЗапросБПЗО.УстановитьПараметр("КонецПериода", КонецДня(ДатаОкончания));
	ЗапросБПЗО.УстановитьПараметр("Счет20011", "2001");
	//ЗапросБПЗО.УстановитьПараметр("КорСчет20", "20");
	ЗапросБПЗО.УстановитьПараметр("КорСчет25", "25");
	ЗапросБПЗО.УстановитьПараметр("КорСчет41", "41");
	ЗапросБПЗО.УстановитьПараметр("Счет10", "10");
	ЗапросБПЗО.УстановитьПараметр("Счет43", "43");
	ЗапросБПЗО.УстановитьПараметр("Счет51", "51");
	ЗапросБПЗО.УстановитьПараметр("Счет52", "52");
	ЗапросБПЗО.УстановитьПараметр("Счет90011", "90011");
	ЗапросБПЗО.УстановитьПараметр("Счет90021", "90021");
	ЗапросБПЗО.УстановитьПараметр("КодЕКДРКурсовыеРазницыПлюс",  Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("CF07060000"));
	ЗапросБПЗО.УстановитьПараметр("КодЕКДРКурсовыеРазницыМинус",  Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду("CF08060000"));
	
	ЗапросБПЗО.Текст = "ВЫБРАТЬ
	|	""Расходы"" КАК ДоходыРасходы,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.СтатьиЗатрат).Код КАК КодЕКДР,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто3 КАК Справочник.Номенклатура).T1_Проект КАК ПроектСсылка,
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК Сумма,
	|	ХозрасчетныйОбороты.Организация.ИНН КАК ОрганизацияИНН,
	|	ХозрасчетныйОбороты.Организация.КПП КАК ОрганизацияКПП,
	//++ T1-2073
	//|	ХозрасчетныйОбороты.Период КАК Месяц,
	|	ХозрасчетныйОбороты.ПериодМесяц КАК Месяц,
	//-- T1-2073
	|	""20011"" КАК Счет,
	//++ T1-2073
	|	ХозрасчетныйОбороты.Регистратор КАК Регистратор,
	//-- T1-2073
	|	""БДР"" КАК ВидБюджета
	|ПОМЕСТИТЬ ВТ_Итоги
	|ИЗ
	//++ T1-2073
	//|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет.КодБыстрогоВыбора = &Счет20011, , Организация В (&Организации), НЕ КорСчет.КодБыстрогоВыбора = &КорСчет25, ) КАК ХозрасчетныйОбороты
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Авто, Счет.КодБыстрогоВыбора = &Счет20011, , Организация В (&Организации), НЕ КорСчет.КодБыстрогоВыбора = &КорСчет25, ) КАК ХозрасчетныйОбороты
	//-- T1-2073
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Доходы"",
	|	ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК Справочник.НоменклатурныеГруппы).тск_КодСтатьи,
	|	Выбор когда ХозрасчетныйОборотыДтКт.Регистратор Ссылка Документ.РеализацияТоваровУслуг тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РеализацияТоваровУслуг).T1_Проект
	|	когда ХозрасчетныйОборотыДтКт.Регистратор Ссылка Документ.КорректировкаРеализации тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.КорректировкаРеализации).ДокументРеализации.T1_Проект
	|	Конец,
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотКт,
	|	ХозрасчетныйОборотыДтКт.Организация.ИНН,
	|	ХозрасчетныйОборотыДтКт.Организация.КПП,
	|	ХозрасчетныйОборотыДтКт.ПериодМесяц,
	|	""90011"",
	//++ T1-2073
	|	ХозрасчетныйОборотыДтКт.Регистратор КАК Регистратор,
	//-- T1-2073
	|	""БДР""
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, , , СчетКт.КодБыстрогоВыбора = &Счет90011, , Организация В (&Организации)) КАК ХозрасчетныйОборотыДтКт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Расходы"",
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.НоменклатурныеГруппы).тск_КодСтатьи,
	|	Выбор когда ХозрасчетныйОбороты.Регистратор Ссылка Документ.РеализацияТоваровУслуг тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).T1_Проект
	|	когда ХозрасчетныйОбороты.Регистратор Ссылка Документ.КорректировкаРеализации тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.КорректировкаРеализации).ДокументРеализации.T1_Проект
	|	Конец,
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Организация.ИНН,
	|	ХозрасчетныйОбороты.Организация.КПП,
	|	ХозрасчетныйОбороты.ПериодМесяц,
	|	""90021"",
	//++ T1-2073
	|	ХозрасчетныйОбороты.Регистратор КАК Регистратор,
	//-- T1-2073
	|	""БДР""
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Авто,
	|			Счет.КодБыстрогоВыбора = &Счет90021,
	|			,
	|			Организация В (&Организации),
	|			КорСчет.КодБыстрогоВыбора = &КорСчет41
	|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &КорСчет41
	|				ИЛИ КорСчет.КодБыстрогоВыбора = &Счет10
	|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &Счет10
	|				ИЛИ КорСчет.КодБыстрогоВыбора = &Счет43
	|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &Счет43,
	|			) КАК ХозрасчетныйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итоги.ДоходыРасходы КАК ДоходыРасходы,
	|	ВТ_Итоги.КодЕКДР КАК КодЕКДР,
	|	ВТ_Итоги.ПроектСсылка КАК ПроектСсылка,
	|	СУММА(ВТ_Итоги.Сумма) КАК Сумма,
	|	ВТ_Итоги.ОрганизацияИНН КАК ОрганизацияИНН,
	|	ВТ_Итоги.ОрганизацияКПП КАК ОрганизацияКПП,
	|	ВТ_Итоги.Месяц КАК Месяц,
	|	ВТ_Итоги.Счет КАК Счет,
	//++ T1-2073
	|	ВТ_Итоги.Регистратор КАК Т1_ПервичныйДокумент,
	//-- T1-2073
	|	ВТ_Итоги.ВидБюджета КАК ВидБюджета
	|ИЗ
	|	ВТ_Итоги КАК ВТ_Итоги
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Итоги.ОрганизацияИНН,
	|	ВТ_Итоги.ОрганизацияКПП,
	|	ВТ_Итоги.Месяц,
	|	ВТ_Итоги.ДоходыРасходы,
	|	ВТ_Итоги.КодЕКДР,
	|	ВТ_Итоги.ПроектСсылка,
	|	ВТ_Итоги.Счет,
	//++ T1-2073
	|	ВТ_Итоги.Регистратор,
	//-- T1-2073
	|	ВТ_Итоги.ВидБюджета
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТ_Итоги.Сумма) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Итоги.ОрганизацияИНН,
	|	ВТ_Итоги.ОрганизацияКПП,
	|	ВТ_Итоги.Месяц,
	|	ВТ_Итоги.КодЕКДР,
	|	ВТ_Итоги.Счет,
	|	ВТ_Итоги.ПроектСсылка
	|ИТОГИ ПО
	|	ОрганизацияИНН,
	|	ОрганизацияКПП,
	|	Месяц,
	|	ДоходыРасходы";
	РезультатЗапроса = ЗапросБПЗО.Выполнить();

	ВыборкаОрганизацияИНН = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаОрганизацияИНН.Следующий() Цикл
		
		ВыборкаОрганизацияКПП = ВыборкаОрганизацияИНН.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОрганизацияКПП.Следующий() Цикл
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИНН", ВыборкаОрганизацияКПП.ОрганизацияИНН);
			//ПараметрыОтбора.Вставить("КПП", ВыборкаОрганизацияКПП.ОрганизацияКПП); По КПП не ищем
			
			НайденныеСтроки = РезультатЗапросаОрганизации.НайтиСтроки(ПараметрыОтбора);
			
			ОрганизацияДляЗагрузки = Справочники.Организации.ПустаяСсылка();
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				
				СтрокаОшибок = СтрокаОшибок+?(СтрокаОшибок = "","",Символы.ПС)+ "Не нашёл организацию " + ВыборкаОрганизацияКПП.ОрганизацияИНН + " " + ВыборкаОрганизацияКПП.ОрганизацияКПП;
				//ВызватьИсключение СтрокаОшибок;
				Продолжить;
			КонецЕсли;
			
			ОрганизацияДляЗагрузки = НайденныеСтроки[0].Ссылка;
			
			ВыборкаМесяц = ВыборкаОрганизацияКПП.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаМесяц.Следующий() Цикл
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ДатаНачала", ВыборкаМесяц.Месяц);
				НайденныеСтроки = РезультатЗапросаПериоды.НайтиСтроки(ПараметрыОтбора);
				ПериодДляЗагрузки = Справочники.Периоды.ПустаяСсылка();
				Если НайденныеСтроки.Количество() = 0 Тогда
					СтрокаОшибок = СтрокаОшибок+?(СтрокаОшибок = "","",Символы.ПС) + "Не нашёл период " + ВыборкаМесяц.Месяц;
					ВызватьИсключение СтрокаОшибок;
				КонецЕсли;
				ПериодДляЗагрузки = НайденныеСтроки[0].Ссылка;
				
				ВыборкаДоходыРасходы = ВыборкаМесяц.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаДоходыРасходы.Следующий() Цикл
					
					Если ВыборкаДоходыРасходы.ДоходыРасходы = "Доходы" Тогда
						ПриходРасход = Перечисления.ВидыДвиженийПриходРасход.Приход;
					Иначе
						ПриходРасход = Перечисления.ВидыДвиженийПриходРасход.Расход;
					КонецЕсли;
					
					
					ЗапросПоискаДокумента = Новый Запрос;
					ЗапросПоискаДокумента.Текст = 
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ОФД.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.ОтражениеФактическихДанныхБюджетирования КАК ОФД
					|ГДЕ
					|	ОФД.Организация = &Организация
					|	И НЕ ОФД.ПометкаУдаления
					|	И ОФД.ПериодБДР = &ПериодБДР
					|	И ОФД.ПериодБДДС = &ПериодБДР
					|	И ОФД.Сценарий = &Сценарий
					|	И ОФД.Комментарий ПОДОБНО &КомментарийПодобно
					|	И ОФД.ПриходРасход = &ПриходРасход";
					ЗапросПоискаДокумента.УстановитьПараметр("Дата", ВыборкаМесяц.Месяц);
					ЗапросПоискаДокумента.УстановитьПараметр("Организация", ОрганизацияДляЗагрузки);
					ЗапросПоискаДокумента.УстановитьПараметр("Сценарий", Сценарий);
					ЗапросПоискаДокумента.УстановитьПараметр("ПериодБДР", ПериодДляЗагрузки);				
					ЗапросПоискаДокумента.УстановитьПараметр("ПриходРасход", ПриходРасход);
					ЗапросПоискаДокумента.УстановитьПараметр("КомментарийПодобно", КомментарийДокумента+"%");
					ВыборкаПоискаДокумента = ЗапросПоискаДокумента.Выполнить().Выбрать();
					
					Если ВыборкаПоискаДокумента.Следующий() Тогда
						НовыйДокумент = ВыборкаПоискаДокумента.Ссылка.ПолучитьОбъект();
						НовыйДокумент.БюджетДоходовИРасходов.Очистить();
						НовыйДокумент.БюджетДвиженияДенежныхСредств.Очистить();
					Иначе
						НовыйДокумент = Документы.ОтражениеФактическихДанныхБюджетирования.СоздатьДокумент();
						НовыйДокумент.Дата 				= ВыборкаМесяц.Месяц;
						НовыйДокумент.Организация 		= ОрганизацияДляЗагрузки;
						НовыйДокумент.Сценарий 			= Сценарий;
						НовыйДокумент.ПериодБДР 		= ПериодДляЗагрузки;
						НовыйДокумент.ПериодБДДС 		= ПериодДляЗагрузки;
						НовыйДокумент.ПриходРасход 		= ПриходРасход;
						НовыйДокумент.Комментарий 		= КомментарийДокумента;
						
					КонецЕсли;
					
					НовыйДокумент.ВалютаДокумента 	= ВалютаРуб;
					НовыйДокумент.ДокументБД 		= ДокументБД;
					НовыйДокумент.ЦФО 				= ОрганизацияДляЗагрузки;
					НовыйДокумент.ЭтоРесурсы 		= Ложь;			
					НовыйДокумент.Ответственный		= Ответственный;
					НовыйДокумент.ОтражатьВоВзаиморасчетах = Ложь;
					
					Выборка = ВыборкаДоходыРасходы.Выбрать();
					
					ЭтоБДДСДокумент = Ложь;
					ЭтоБДРДокумент 	= Ложь;
					
					Пока Выборка.Следующий() Цикл
						
						 ПроектСсылка = Выборка.ПроектСсылка;
						Если Выборка.ВидБюджета = "БДР" Тогда
							ЭтоБДРДокумент 	= Истина; 
							НоваяСтрока = НовыйДокумент.БюджетДоходовИРасходов.Добавить();
							Если Выборка.Счет = "90021"  Тогда
								НоваяСтрока.СтатьяДоходовИРасходов 	= СоответствиеСтатейБДР.Получить(Выборка.КодЕКДР);
							Иначе
								НоваяСтрока.СтатьяДоходовИРасходов 	= Справочники.СтатьиДоходовИРасходов.НайтиПоКоду(СокрЛП(Строка(Выборка.КодЕКДР)));
							КонецЕсли;  
							
							//++ T1-949
							СтрБДРЦФО = РезультатЗапросаСтатьяБДРЦФО.Найти(НоваяСтрока.СтатьяДоходовИРасходов,"СтатьяБДР"); 
							Если ЗначениеЗаполнено(СтрБДРЦФО) Тогда
								НоваяСтрока.ЦФО = СтрБДРЦФО.ЦФО;
							Иначе
								НоваяСтрока.ЦФО = Справочники.Организации.ПустаяСсылка();
							КонецЕсли;
							//НоваяСтрока.ЦФО 					= ОрганизацияДляЗагрузки; 
							//-- T1-949

							НоваяСтрока.Проект 					= ПроектСсылка;
							НоваяСтрока.Сумма 					= Выборка.Сумма;
							НоваяСтрока.СуммаБезНДС 			= Выборка.Сумма;
							НоваяСтрока.СуммаВзаиморасчетов 	= Выборка.Сумма;
							НоваяСтрока.КурсВзаиморасчетов 		= 1;
							НоваяСтрока.КратностьВзаиморасчетов = 1;
							//++ T1-2073
							НоваяСтрока.Т1_ПервичныйДокумент 	= Выборка.Т1_ПервичныйДокумент;
							//-- T1-2073
						Иначе
							ЭтоБДДСДокумент = Истина;
							НоваяСтрока = НовыйДокумент.БюджетДвиженияДенежныхСредств.Добавить();
							//НоваяСтрока.СтатьяДвиженияДенежныхСредств	= Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду(Выборка.КодЕКДР);
							НоваяСтрока.СтатьяДвиженияДенежныхСредств	= Выборка.КодЕКДР;
						   
							//++ T1-949
							СтрБДДСЦФО = РезультатЗапросаСтатьяБДДСЦФО.Найти(НоваяСтрока.СтатьяДвиженияДенежныхСредств, "СтатьяБДДС"); 
							Если ЗначениеЗаполнено(СтрБДДСЦФО) Тогда
								НоваяСтрока.ЦФО = СтрБДДСЦФО.ЦФО;
							Иначе
								НоваяСтрока.ЦФО = Справочники.Организации.ПустаяСсылка();
							КонецЕсли;
							//НоваяСтрока.ЦФО 					= ОрганизацияДляЗагрузки; 
							//-- T1-949   
							
							НоваяСтрока.Проект 					= ПроектСсылка;
							НоваяСтрока.Сумма 					= Выборка.Сумма;
							НоваяСтрока.СуммаВзаиморасчетов 	= Выборка.Сумма;
							НоваяСтрока.КурсВзаиморасчетов 		= 1;
							НоваяСтрока.КратностьВзаиморасчетов = 1;
						КонецЕсли;
						
					КонецЦикла;
					НовыйДокумент.ЭтоБДДС 			= ЭтоБДДСДокумент;
					НовыйДокумент.ЭтоБДР 			= ЭтоБДРДокумент;
					
					НовыйДокумент.СуммаДокумента = НовыйДокумент.БюджетДоходовИРасходов.Итог("Сумма") + НовыйДокумент.БюджетДвиженияДенежныхСредств.Итог("Сумма"); 
					
					Обработано = Обработано + 1;
					
					НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
					
					Загружено = Загружено + 1;
				КонецЦикла; //ВыборкаДоходыРасходы	
			КонецЦикла; //ВыборкааМесяц
		КонецЦикла; //Выборка КПП
	КонецЦикла; //Выборка ИНН

	//УстановитьДатуПоследнейЗагрузки(Система,ЗапросСсылка,ДатаНачалаЗагрузки);

	СтрокаОшибок = СтрокаОшибок + ?(СтрокаОшибок="","",Символы.ПС) +  ": Обработано - "+Обработано+", Загружено - "+Загружено;
',
  'Комментарий': ''
 },
 'ТЧ': [
  {
   'Переменные': []
  }
 ]
}
