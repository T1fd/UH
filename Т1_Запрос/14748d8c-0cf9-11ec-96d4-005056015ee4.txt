{
 'Реквизиты': {
  'Наименование': 'Загрузка факта БДР БДДС ТСИ',
  'ТекстОбработки': '	//настройки Т1-539
	МесяцЗагрузки = Текущаядата();
	МесяцевЗагрузки = 2;
	ЗагружатьДанныеТолькоПоПроектам = Истина;
	
	СоответствиеСтатейБДР = Новый Соответствие;
	СоответствиеСтатейБДР.Вставить("05010101", Справочники.СтатьиДоходовИРасходов.НайтиПоКоду("11020100"));
	СоответствиеСтатейБДР.Вставить("05010102", Справочники.СтатьиДоходовИРасходов.НайтиПоКоду("11020100"));
	СоответствиеСтатейБДР.Вставить("05020000", Справочники.СтатьиДоходовИРасходов.НайтиПоКоду("11020200"));

	//переменные
	ВалютаРуб = Константы.ВалютаРегламентированногоУчета.Получить();
	ДокументБД = Справочники.ДокументыБД.НайтиПоНаименованию("Операция",,,Справочники.ТипыБазДанных.ТекущаяИБ);
	Сценарий = Справочники.Сценарии.Факт;
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КомментарийДокумента = "#ЗагруженоИзБП";

	//вычисляемые переменные
	ДатаНачала = НачалоМесяца(ДобавитьМесяц(МесяцЗагрузки,1-МесяцевЗагрузки));
	ДатаОкончания = КонецМесяца(МесяцЗагрузки);

	ДатаНачалаЗагрузки = Текущаядата();
	Обработано = 0;
	Загружено = 0;


	//Список организаций текущей системы
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.КПП КАК КПП,
	|	Организации.ИНН КАК ИНН
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ПометкаУдаления = ЛОЖЬ";	
	РезультатЗапросаОрганизации = Запрос.Выполнить().Выгрузить();

	//Периоды текущей системы
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Периоды.Ссылка КАК Ссылка,
	|	Периоды.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	Справочник.Периоды КАК Периоды
	|ГДЕ
	|	Периоды.ПометкаУдаления = ЛОЖЬ
	|	И Периоды.Периодичность = &Месяц";
	Запрос.УстановитьПараметр("Месяц", Перечисления.Периодичность.Месяц);
	РезультатЗапросаПериоды = Запрос.Выполнить().Выгрузить();
         
    //++ T1-949
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ\r
	|	Т1_СтатьиБДСПоЦФО.СтатьяБДС КАК СтатьяБДДС,\r
	|	МИНИМУМ(Т1_СтатьиБДСПоЦФО.ЦФО) КАК ЦФО\r
	|ИЗ\r
	|	РегистрСведений.Т1_СтатьиБДСПоЦФО КАК Т1_СтатьиБДСПоЦФО\r
	|\r
	|СГРУППИРОВАТЬ ПО\r
	|	Т1_СтатьиБДСПоЦФО.СтатьяБДС";
	РезультатЗапросаСтатьяБДДСЦФО = Запрос.Выполнить().Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ\r
	|	Т1_СтатьиБДРПоЦФО.СтатьяБДР КАК СтатьяБДР,\r
	|	МИНИМУМ(Т1_СтатьиБДРПоЦФО.ЦФО) КАК ЦФО\r
	|ИЗ\r
	|	РегистрСведений.Т1_СтатьиБДРПоЦФО КАК Т1_СтатьиБДРПоЦФО       
	|\r	|СГРУППИРОВАТЬ ПО\r
	|	Т1_СтатьиБДРПоЦФО.СтатьяБДР";
	РезультатЗапросаСтатьяБДРЦФО = Запрос.Выполнить().Выгрузить();
    //-- T1-949
    
	//Запрос в БП
	ЗапросБПЗО = Соединение.NewObject("Запрос");
	ЗапросБПЗО.УстановитьПараметр("ЗагружатьДанныеТолькоПоПроектам", ЗагружатьДанныеТолькоПоПроектам);
	ЗапросБПЗО.УстановитьПараметр("НачалоПериода", НачалоДня(ДатаНачала));
	ЗапросБПЗО.УстановитьПараметр("КонецПериода", КонецДня(ДатаОкончания));
	ЗапросБПЗО.УстановитьПараметр("Счет20011", "2001");
	//ЗапросБПЗО.УстановитьПараметр("КорСчет20", "20");
	ЗапросБПЗО.УстановитьПараметр("КорСчет25", "25");
	ЗапросБПЗО.УстановитьПараметр("КорСчет41", "41");
	ЗапросБПЗО.УстановитьПараметр("Счет10", "10");
	ЗапросБПЗО.УстановитьПараметр("Счет43", "43");
	ЗапросБПЗО.УстановитьПараметр("Счет51", "51");
	ЗапросБПЗО.УстановитьПараметр("Счет52", "52");
	ЗапросБПЗО.УстановитьПараметр("Счет90011", "90011");
	ЗапросБПЗО.УстановитьПараметр("Счет90021", "90021");
	//++ Т1-835
	ЗапросБПЗО.УстановитьПараметр("КодЕКДРКурсовыеРазницыПлюс", "CF07060000");
	ЗапросБПЗО.УстановитьПараметр("КодЕКДРКурсовыеРазницыМинус", "CF08060000");
	//-- Т1-835
	
	ЗапросБПЗО.Текст = "ВЫБРАТЬ
	|	""Расходы"" КАК ДоходыРасходы,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.СтатьиЗатрат).тск_КодСтатьи КАК КодЕКДР,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто3 КАК Справочник.Номенклатура).T1_Проект КАК ПроектСсылка,
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК Сумма,
	|	ХозрасчетныйОбороты.Организация.ИНН КАК ОрганизацияИНН,
	|	ХозрасчетныйОбороты.Организация.КПП КАК ОрганизацияКПП,
	|	ХозрасчетныйОбороты.Период КАК Месяц,
	|	""20011"" КАК Счет,
	|	""БДР"" КАК ВидБюджета
	|ПОМЕСТИТЬ ВТ_Итоги
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет.КодБыстрогоВыбора = &Счет20011, , , НЕ КорСчет.КодБыстрогоВыбора = &КорСчет25, ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Расходы"",
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.СтатьиЗатрат).тск_КодСтатьи,
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто3 КАК Справочник.Номенклатура).T1_Проект,
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Организация.ИНН,
	|	ХозрасчетныйОбороты.Организация.КПП,
	|	ХозрасчетныйОбороты.Период,
	|	""20011"",
	|	""БДР""
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, Месяц, Счет.КодБыстрогоВыбора = &Счет20011, , , КорСчет.КодБыстрогоВыбора = &КорСчет25, ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Доходы"",
	|	ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.СубконтоКт1 КАК Справочник.НоменклатурныеГруппы).тск_КодСтатьи,
	//++ Т1-808
	//|		ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РеализацияТоваровУслуг).T1_Проект,
	|	Выбор когда ХозрасчетныйОборотыДтКт.Регистратор Ссылка Документ.РеализацияТоваровУслуг тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.РеализацияТоваровУслуг).T1_Проект
	|	когда ХозрасчетныйОборотыДтКт.Регистратор Ссылка Документ.КорректировкаРеализации тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОборотыДтКт.Регистратор КАК Документ.КорректировкаРеализации).ДокументРеализации.T1_Проект
	|	Конец,
	//-- Т1-808
	|	ХозрасчетныйОборотыДтКт.СуммаНУОборотКт,
	|	ХозрасчетныйОборотыДтКт.Организация.ИНН,
	|	ХозрасчетныйОборотыДтКт.Организация.КПП,
	|	ХозрасчетныйОборотыДтКт.ПериодМесяц,
	|	""90011"",
	|	""БДР""
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ОборотыДтКт(&НачалоПериода, &КонецПериода, Авто, , , СчетКт.КодБыстрогоВыбора = &Счет90011, , ) КАК ХозрасчетныйОборотыДтКт
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Расходы"",
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто1 КАК Справочник.НоменклатурныеГруппы).тск_КодСтатьи,
	//++ Т1-808
	//|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).T1_Проект,
	|	Выбор когда ХозрасчетныйОбороты.Регистратор Ссылка Документ.РеализацияТоваровУслуг тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.РеализацияТоваровУслуг).T1_Проект
	|	когда ХозрасчетныйОбороты.Регистратор Ссылка Документ.КорректировкаРеализации тогда
	|		ВЫРАЗИТЬ(ХозрасчетныйОбороты.Регистратор КАК Документ.КорректировкаРеализации).ДокументРеализации.T1_Проект
	|	Конец,
	//-- Т1-808
	|	ХозрасчетныйОбороты.СуммаОборот,
	|	ХозрасчетныйОбороты.Организация.ИНН,
	|	ХозрасчетныйОбороты.Организация.КПП,
	|	ХозрасчетныйОбороты.ПериодМесяц,
	|	""90021"",
	|	""БДР""
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Авто,
	|			Счет.КодБыстрогоВыбора = &Счет90021,
	|			,
	|			,
	|			КорСчет.КодБыстрогоВыбора = &КорСчет41
	|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &КорСчет41
	|				ИЛИ КорСчет.КодБыстрогоВыбора = &Счет10
	|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &Счет10
	|				ИЛИ КорСчет.КодБыстрогоВыбора = &Счет43
	|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &Счет43,
	|			) КАК ХозрасчетныйОбороты
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	""Расходы"",
	//|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.СтатьиЗатрат).тск_КодСтатьи,
	//|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто3 КАК Справочник.Номенклатура).T1_Проект,
	//|	ХозрасчетныйОбороты.СуммаОборот,
	//|	ХозрасчетныйОбороты.Организация.ИНН,
	//|	ХозрасчетныйОбороты.Организация.КПП,
	//|	ХозрасчетныйОбороты.ПериодМесяц,
	//|	""90021_20"",
	//|	""БДР""
	//|ИЗ
	//|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	//|			&НачалоПериода,
	//|			&КонецПериода,
	//|			Авто,
	//|			Счет.КодБыстрогоВыбора = &Счет90021,
	//|			,
	//|			,
	//|			КорСчет.КодБыстрогоВыбора = &КорСчет20
	//|				ИЛИ КорСчет.Родитель.КодБыстрогоВыбора = &КорСчет20,
	//|			) КАК ХозрасчетныйОбороты
	//|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Доходы"",
	//++ Т1-835
	//|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.СтатьиДвиженияДенежныхСредств).тск_КодCF,
	|	Выбор когда ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто1 КАК Справочник.ПрочиеДоходыИРасходы) = Значение(Справочник.ПрочиеДоходыИРасходы.КурсовыеРазницы) Тогда
	|		выбор когда ХозрасчетныйОбороты.СуммаОборотДт > 0 Тогда &КодЕКДРКурсовыеРазницыПлюс Иначе &КодЕКДРКурсовыеРазницыМинус Конец
	|	Иначе ВЫРАЗИТЬ(ХозрасчетныйОбороты.Субконто2 КАК Справочник.СтатьиДвиженияДенежныхСредств).тск_КодCF конец,
	//-- Т1-835
	|	ВЫРАЗИТЬ(ХозрасчетныйОбороты.КорСубконто2 КАК Справочник.ДоговорыКонтрагентов).тск_ЭтапПроекта,
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	ХозрасчетныйОбороты.Организация.ИНН,
	|	ХозрасчетныйОбороты.Организация.КПП,
	|	ХозрасчетныйОбороты.ПериодМесяц,
	|	""51"",
	|	""БДДС""
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			Авто,
	|			Счет.КодБыстрогоВыбора = &Счет51
	|				ИЛИ Счет.Родитель.КодБыстрогоВыбора = &Счет51
	|				ИЛИ Счет.КодБыстрогоВыбора = &Счет52
	|				ИЛИ Счет.Родитель.КодБыстрогоВыбора = &Счет52,
	|			,
	|			,
	|			,
	|			) КАК ХозрасчетныйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Итоги.ДоходыРасходы КАК ДоходыРасходы,
	|	ВТ_Итоги.КодЕКДР КАК КодЕКДР,
	|	ВТ_Итоги.ПроектСсылка КАК ПроектСсылка,
	|	СУММА(ВТ_Итоги.Сумма) КАК Сумма,
	|	ВТ_Итоги.ОрганизацияИНН КАК ОрганизацияИНН,
	|	ВТ_Итоги.ОрганизацияКПП КАК ОрганизацияКПП,
	|	ВТ_Итоги.Месяц КАК Месяц,
	|	ВТ_Итоги.Счет КАК Счет,
	|	ВТ_Итоги.ВидБюджета КАК ВидБюджета
	|ИЗ
	|	ВТ_Итоги КАК ВТ_Итоги
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Итоги.ОрганизацияИНН,
	|	ВТ_Итоги.ОрганизацияКПП,
	|	ВТ_Итоги.Месяц,
	|	ВТ_Итоги.ДоходыРасходы,
	|	ВТ_Итоги.КодЕКДР,
	|	ВТ_Итоги.ПроектСсылка,
	|	ВТ_Итоги.Счет,
	|	ВТ_Итоги.ВидБюджета
	|
	//++ Т1-835
	|ИМЕЮЩИЕ
	|	СУММА(ВТ_Итоги.Сумма) <> 0
	//-- Т1-835
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_Итоги.ОрганизацияИНН,
	|	ВТ_Итоги.ОрганизацияКПП,
	|	ВТ_Итоги.Месяц,
	|	ВТ_Итоги.КодЕКДР,
	|	ВТ_Итоги.Счет,
	|	ВТ_Итоги.ПроектСсылка
	|ИТОГИ ПО
	|	ОрганизацияИНН,
	|	ОрганизацияКПП,
	|	Месяц,
	|	ДоходыРасходы";
	РезультатЗапроса = ЗапросБПЗО.Выполнить();

	ВыборкаОрганизацияИНН = РезультатЗапроса.Выбрать(Соединение.ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаОрганизацияИНН.Следующий() Цикл
		
		ВыборкаОрганизацияКПП = ВыборкаОрганизацияИНН.Выбрать(Соединение.ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаОрганизацияКПП.Следующий() Цикл
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИНН", ВыборкаОрганизацияКПП.ОрганизацияИНН);
			//ПараметрыОтбора.Вставить("КПП", ВыборкаОрганизацияКПП.ОрганизацияКПП); По КПП не ищем
			
			НайденныеСтроки = РезультатЗапросаОрганизации.НайтиСтроки(ПараметрыОтбора);
			
			ОрганизацияДляЗагрузки = Справочники.Организации.ПустаяСсылка();
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				
				СтрокаОшибок = СтрокаОшибок+?(СтрокаОшибок = "","",Символы.ПС)+ "Не нашёл организацию " + ВыборкаОрганизацияКПП.ОрганизацияИНН + " " + ВыборкаОрганизацияКПП.ОрганизацияКПП;
				//ВызватьИсключение СтрокаОшибок;
				Продолжить;
			КонецЕсли;
			
			ОрганизацияДляЗагрузки = НайденныеСтроки[0].Ссылка;
			
			ВыборкаМесяц = ВыборкаОрганизацияКПП.Выбрать(Соединение.ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаМесяц.Следующий() Цикл
				
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ДатаНачала", ВыборкаМесяц.Месяц);
				НайденныеСтроки = РезультатЗапросаПериоды.НайтиСтроки(ПараметрыОтбора);
				ПериодДляЗагрузки = Справочники.Периоды.ПустаяСсылка();
				Если НайденныеСтроки.Количество() = 0 Тогда
					СтрокаОшибок = СтрокаОшибок+?(СтрокаОшибок = "","",Символы.ПС) + "Не нашёл период " + ВыборкаМесяц.Месяц;
					ВызватьИсключение СтрокаОшибок;
				КонецЕсли;
				ПериодДляЗагрузки = НайденныеСтроки[0].Ссылка;
				
				ВыборкаДоходыРасходы = ВыборкаМесяц.Выбрать(Соединение.ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаДоходыРасходы.Следующий() Цикл
					
					Если ВыборкаДоходыРасходы.ДоходыРасходы = "Доходы" Тогда
						ПриходРасход = Перечисления.ВидыДвиженийПриходРасход.Приход;
					Иначе
						ПриходРасход = Перечисления.ВидыДвиженийПриходРасход.Расход;
					КонецЕсли;
					
					
					ЗапросПоискаДокумента = Новый Запрос;
					ЗапросПоискаДокумента.Текст = 
					"ВЫБРАТЬ ПЕРВЫЕ 1
					|	ОФД.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.ОтражениеФактическихДанныхБюджетирования КАК ОФД
					|ГДЕ
					|	ОФД.Организация = &Организация
					|	И НЕ ОФД.ПометкаУдаления
					|	И ОФД.ПериодБДР = &ПериодБДР
					|	И ОФД.ПериодБДДС = &ПериодБДР
					|	И ОФД.Сценарий = &Сценарий
					|	И ОФД.Комментарий ПОДОБНО &КомментарийПодобно
					|	И ОФД.ПриходРасход = &ПриходРасход";
					ЗапросПоискаДокумента.УстановитьПараметр("Дата", ВыборкаМесяц.Месяц);
					ЗапросПоискаДокумента.УстановитьПараметр("Организация", ОрганизацияДляЗагрузки);
					ЗапросПоискаДокумента.УстановитьПараметр("Сценарий", Сценарий);
					ЗапросПоискаДокумента.УстановитьПараметр("ПериодБДР", ПериодДляЗагрузки);				
					ЗапросПоискаДокумента.УстановитьПараметр("ПриходРасход", ПриходРасход);
					ЗапросПоискаДокумента.УстановитьПараметр("КомментарийПодобно", КомментарийДокумента+"%");
					ВыборкаПоискаДокумента = ЗапросПоискаДокумента.Выполнить().Выбрать();
					
					Если ВыборкаПоискаДокумента.Следующий() Тогда
						НовыйДокумент = ВыборкаПоискаДокумента.Ссылка.ПолучитьОбъект();
						НовыйДокумент.БюджетДоходовИРасходов.Очистить();
						НовыйДокумент.БюджетДвиженияДенежныхСредств.Очистить();
					Иначе
						НовыйДокумент = Документы.ОтражениеФактическихДанныхБюджетирования.СоздатьДокумент();
						НовыйДокумент.Дата 				= ВыборкаМесяц.Месяц;
						НовыйДокумент.Организация 		= ОрганизацияДляЗагрузки;
						НовыйДокумент.Сценарий 			= Сценарий;
						НовыйДокумент.ПериодБДР 		= ПериодДляЗагрузки;
						НовыйДокумент.ПериодБДДС 		= ПериодДляЗагрузки;
						НовыйДокумент.ПриходРасход 		= ПриходРасход;
						НовыйДокумент.Комментарий 		= КомментарийДокумента;
						
					КонецЕсли;
					
					НовыйДокумент.ВалютаДокумента 	= ВалютаРуб;
					НовыйДокумент.ДокументБД 		= ДокументБД;
					НовыйДокумент.ЦФО 				= ОрганизацияДляЗагрузки;
					НовыйДокумент.ЭтоРесурсы 		= Ложь;			
					НовыйДокумент.Ответственный		= Ответственный;
					НовыйДокумент.ОтражатьВоВзаиморасчетах = Ложь;
					
					Выборка = ВыборкаДоходыРасходы.Выбрать();
					
					ЭтоБДДСДокумент = Ложь;
					ЭтоБДРДокумент 	= Ложь;
					
					Пока Выборка.Следующий() Цикл
						
						ПроектГУИД = Соединение.XMLСтрока(Выборка.ПроектСсылка);
						Если ПроектГУИД = "" или ПроектГУИД = "00000000-0000-0000-0000-000000000000" Тогда
							ПроектСсылка = Справочники.Проекты.ПустаяСсылка();
						Иначе
							ПроектСсылка = Справочники.Проекты.ПолучитьСсылку(Новый УникальныйИдентификатор(ПроектГУИД));	
						КонецЕсли;
						
						Если Выборка.ВидБюджета = "БДР" Тогда
							ЭтоБДРДокумент 	= Истина; 
							НоваяСтрока = НовыйДокумент.БюджетДоходовИРасходов.Добавить();
							Если Выборка.Счет = "90021" Тогда
								НоваяСтрока.СтатьяДоходовИРасходов 	= СоответствиеСтатейБДР.Получить(Выборка.КодЕКДР);
							Иначе
								НоваяСтрока.СтатьяДоходовИРасходов 	= Справочники.СтатьиДоходовИРасходов.НайтиПоКоду(Выборка.КодЕКДР);
							КонецЕсли;  
							
							//++ T1-949
							СтрБДРЦФО = РезультатЗапросаСтатьяБДРЦФО.Найти(НоваяСтрока.СтатьяДоходовИРасходов,"СтатьяБДР"); 
							Если ЗначениеЗаполнено(СтрБДРЦФО) Тогда
								НоваяСтрока.ЦФО = СтрБДРЦФО.ЦФО;
							Иначе
								НоваяСтрока.ЦФО = Справочники.Организации.ПустаяСсылка();
							КонецЕсли;
							//НоваяСтрока.ЦФО 					= ОрганизацияДляЗагрузки; 
							//-- T1-949

							НоваяСтрока.Проект 					= ПроектСсылка;
							НоваяСтрока.Сумма 					= Выборка.Сумма;
							НоваяСтрока.СуммаБезНДС 			= Выборка.Сумма;
							НоваяСтрока.СуммаВзаиморасчетов 	= Выборка.Сумма;
							НоваяСтрока.КурсВзаиморасчетов 		= 1;
							НоваяСтрока.КратностьВзаиморасчетов = 1;
						Иначе
							ЭтоБДДСДокумент = Истина;
							НоваяСтрока = НовыйДокумент.БюджетДвиженияДенежныхСредств.Добавить();
							НоваяСтрока.СтатьяДвиженияДенежныхСредств	= Справочники.СтатьиДвиженияДенежныхСредств.НайтиПоКоду(Выборка.КодЕКДР);
						
							//++ T1-949
							СтрБДДСЦФО = РезультатЗапросаСтатьяБДДСЦФО.Найти(НоваяСтрока.СтатьяДвиженияДенежныхСредств, "СтатьяБДДС"); 
							Если ЗначениеЗаполнено(СтрБДДСЦФО) Тогда
								НоваяСтрока.ЦФО = СтрБДДСЦФО.ЦФО;
							Иначе
								НоваяСтрока.ЦФО = Справочники.Организации.ПустаяСсылка();
							КонецЕсли;
							//НоваяСтрока.ЦФО 					= ОрганизацияДляЗагрузки; 
							//-- T1-949   
							
							НоваяСтрока.Проект 					= ПроектСсылка;
							НоваяСтрока.Сумма 					= Выборка.Сумма;
							НоваяСтрока.СуммаВзаиморасчетов 	= Выборка.Сумма;
							НоваяСтрока.КурсВзаиморасчетов 		= 1;
							НоваяСтрока.КратностьВзаиморасчетов = 1;
						КонецЕсли;
						
					КонецЦикла;
					НовыйДокумент.ЭтоБДДС 			= ЭтоБДДСДокумент;
					НовыйДокумент.ЭтоБДР 			= ЭтоБДРДокумент;
					
					НовыйДокумент.СуммаДокумента = НовыйДокумент.БюджетДоходовИРасходов.Итог("Сумма") + НовыйДокумент.БюджетДвиженияДенежныхСредств.Итог("Сумма"); 
					
					Обработано = Обработано + 1;
					
					НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
					
					Загружено = Загружено + 1;
				КонецЦикла; //ВыборкаДоходыРасходы	
			КонецЦикла; //ВыборкаМесяц
		КонецЦикла; //Выборка КПП
	КонецЦикла; //Выборка ИНН

	УстановитьДатуПоследнейЗагрузки(Система,ЗапросСсылка,ДатаНачалаЗагрузки);

	СтрокаОшибок = СтрокаОшибок + ?(СтрокаОшибок="","",Символы.ПС)+Система +  ": Обработано - "+Обработано+", Загружено - "+Загружено;',
  'Комментарий': ''
 },
 'ТЧ': [
  {
   'Переменные': []
  }
 ]
}
